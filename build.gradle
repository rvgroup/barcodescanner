ext {
    versions = [
            compile_sdk    : 31,
            min_sdk        : 14,
            min_compile_sdk: 31,
            target_sdk     : 31,
            version_code   : 1914,
            version_name   : "1.9.14",
            zxing          : "3.3.3",
            annotaions     : "1.3.0",
            app_compat     : "1.4.1",
            material       : "1.5.0"
    ]

    libraries = [
            app_compat          : "androidx.appcompat:appcompat:$versions.app_compat",
            android_annotations : "androidx.annotation:annotation:$versions.annotaions",
            material            : "com.google.android.material:material:$versions.material",
            zxing_core          : "com.google.zxing:core:$versions.zxing"
    ]
}

buildscript {
    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

allprojects {
    group = 'me.dm7.barcodescanner'
    version = versions.version_name

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        def isAndroidLibrary = "com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)
        def isAndroidApp = "com.android.build.gradle.AppPlugin".equals(plugin.class.name)

        if (isAndroidLibrary || isAndroidApp) {
            project.android {
                compileSdkVersion versions.compile_sdk

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                android {
                    lintOptions {
                        abortOnError false
                    }
                }

                defaultConfig {
                    aarMetadata {
                        minCompileSdk = versions.min_compile_sdk
                    }
                    minSdkVersion versions.min_sdk
                    targetSdkVersion versions.target_sdk
                    versionCode versions.version_code
                    versionName versions.version_name
                }
            }
        }

        if (isAndroidLibrary) {
            afterEvaluate {
                publishing {
                    publications {
                        release(MavenPublication) {
                            from components.release

                            groupId = 'com.github.Oknesif.barcodescanner'
                            artifactId = pomArtifactId
                            version project.version

                            pom {
                                name = pomName
                                description = pomDescription
                            }
                        }
                    }
                }
            }
        }
    }
}
